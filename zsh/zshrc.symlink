#! /usr/bin/env zsh
#
# ------------------------------------------------------------------------------
# INIT
# ------------------------------------------------------------------------------

# Use .localrc for SUPER SECRET CRAP that you don't want in yor public,
# versioned repo.
[ -f ~/.localrc ]  && . ~/.localrc

# ------------------------------------------------------------------------------
# EXPORT
# ------------------------------------------------------------------------------

export LC_ALL="en_US.UTF-8"
export LANG="en_US.UTF-8"
export PATH="/usr/local/bin:$PATH"
export GPG_TTY=$(tty)

export DOTFILES="$HOME/dotfiles"
export PROJECTS="$HOME/dev"

export EDITOR="vim"
export LSCOLORS="Gxfxcxdxbxegedabagacad"
export CLICOLOR=1

# ------------------------------------------------------------------------------
# CONFIG
# ------------------------------------------------------------------------------

# All our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# Load the path files.
for file in  ${(M)config_files:#*/path.zsh}; do
	source  "$file"
done

# Load anitbody plugins.
#source ~/.zsh_plugins.sh

# Load everything but the path and completion files.
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
	source "$file"
done

# ------------------------------------------------------------------------------
# PROMPT
# ------------------------------------------------------------------------------

autoload -U colors && colors

# ------------------------------------------------------------------------------
# AUTCOMPLETION
# ------------------------------------------------------------------------------

# enable completion
autoload -Uz compinit
compinit

autoload bashcompinit
bashcompinit

zmodload -i zsh/complist

WORDCHARS=''

unsetopt menu_complete   # do not autoselect the first completion entry
unsetopt flowcontrol
setopt auto_menu         # show completion menu on successive tab press
setopt complete_in_word
setopt always_to_end

# autocompletion with an arrow-key driven interface
zstyle ':completion:*:*:*:*:*' menu select

zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' list-colors ''
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'

zstyle ':completion:*:*:*:*:processes' command "ps -u $USER -o pid,user,comm -w -w"

# Don't complete uninteresting users
zstyle ':completion:*:*:*:users' ignored-patterns \
        adm amanda apache at avahi avahi-autoipd beaglidx bin cacti canna \
        clamav daemon dbus distcache dnsmasq dovecot fax ftp games gdm \
        gkrellmd gopher hacluster haldaemon halt hsqldb ident junkbust kdm \
        ldap lp mail mailman mailnull man messagebus  mldonkey mysql nagios \
        named netdump news nfsnobody nobody nscd ntp nut nx obsrun openvpn \
        operator pcap polkitd postfix postgres privoxy pulse pvm quagga radvd \
        rpc rpcuser rpm rtkit scard shutdown squid sshd statd svn sync tftp \
        usbmux uucp vcsa wwwrun xfs '_*'

zstyle '*' single-ignored show

# Automatically update PATH entries
zstyle ':completion:*' rehash true

# Keep directories and files separated
zstyle ':completion:*' list-dirs-first true

# Load every completion after autcomplete loads.
for file in ${(M)config_files:#*/completion.zsh}; do
	source "$file"
done

unset config_files updated_at


# ------------------------------------------------------------------------------
# PLUGINS
# ------------------------------------------------------------------------------

source "$HOME/.zplugin/bin/zplugin.zsh"
autoload -Uz _zplugin
(( ${+_comps} )) && _comps[zplugin]=_zplugin

zplugin light romkatv/powerlevel10k
zplugin snippet https://github.com/sainnhe/dotfiles/blob/master/.zsh-theme-gruvbox-material-dark

zplugin light zsh-users/zsh-completions
