#! /usr/bin/env zsh

# Use .localrc for SUPER SECRET CRAP that you don't want in yor public,
# versioned repo.
[[ -f ~/.localrc ]] && source ~/.localrc

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/dotfiles"

# project folder that we can `c [tab]` to
export PROJECTS="$HOME/dev"

# All our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# Load the path files.
for file in  ${(M)config_files:#*/path.zsh}; do
	source  "$file"
done

# Load everything but the path and completion files.
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
	source "$file"
done

autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
  compinit
done
compinit -C

autoload -U +X bashcompinit && bashcompinit

# Load every completion after autcomplete loads.
for file in ${(M)config_files:#*/completion.zsh}; do
	source "$file"
done

unset config_files updated_at

source "$HOME/.zplugin/bin/zplugin.zsh"
autoload -Uz _zplugin
(( ${+_comps} )) && _comps[zplugin]=_zplugin

zplugin light romkatv/powerlevel10k
zplugin light zsh-users/zsh-completions

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ -f ~/.p10k.zsh ]] && source ~/.p10k.zsh
